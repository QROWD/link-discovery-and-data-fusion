#!sparql-integrate --jq --flat --depth=4

PREFIX  owl:  <http://www.w3.org/2002/07/owl#>
PREFIX  geos: <http://www.opengis.net/ont/geosparql#>
PREFIX  lgdo: <http://linkedgeodata.org/ontology/>
PREFIX  rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX  dct:  <http://purl.org/dc/terms/>
PREFIX  dbpo: <http://dbpedia.org/ontology/>
PREFIX  prov: <http://www.w3.org/ns/prov#>

INSERT {
 <C> <in> [ <type> "Feature" ;
   <properties> [
    <id> ?idstr_s ;
    <n_posti> ?capacity_s ;
    <fid> ?fid_s ; # "rastrelliere.0"
    <via> ?street_s ; # "passaggio Peterlongo"
    <zona> ?zone_s ; # "ZTL"
    <tipologia> ?topo_s ; # "monofacciale"
    <fumetto> ?label_s ; # "passaggio Peterlongo  n.posti  10  -  monofacciale"
    <descrizione> ?desc_s ; # "passaggio Peterlongo  n.posti  10  -  monofacciale"
    <provenance> ?prov_str ; # 'Municipality'
   ] ;
   <geometry> ?geom_s ;
 ]
} WHERE {
   { SELECT ?id
   (SAMPLE(?geom) as ?geom_s)
   (SAMPLE(?capacity) as ?capacity_s)
   (SAMPLE(?topo) as ?topo_s)
   (SAMPLE(?desc) as ?desc_s)
   (SAMPLE(?label) as ?label_s)
   (SAMPLE(?fid) as ?fid_s)
   (SAMPLE(?idstr) as ?idstr_s)
   (SAMPLE(?street) as ?street_s)
   (SAMPLE(?zone) as ?zone_s)
   (GROUP_CONCAT(DISTINCT ?prov ; separator=",") as ?prov_str)
   WHERE {
  ?id a <http://qrowd-project.eu/ontology/Representative> .
  ?id geos:geometry/geos:asWKT ?geom .
    
  OPTIONAL { ?id lgdo:capacity ?capacity } .
  OPTIONAL { ?id lgdo:bicycle_parking ?topo } .
  OPTIONAL { ?id rdfs:comment ?desc } . 
  OPTIONAL { ?id rdfs:label ?label } .
  OPTIONAL {  ?id dct:id ?fid  } .
  OPTIONAL {  ?id lgdo:near/rdfs:label ?street  } .
  OPTIONAL {  ?id dbpo:district ?zone  } .
  OPTIONAL {  ?id prov:wasDerivedFrom ?deriv .
      BIND(STRAFTER(STR(?deriv), "http://qrowd-project.eu/resource/dataset-") as ?prov) .
  }
    BIND(STR(?id) as ?idstr) .
  } GROUP BY ?id }
}

INSERT DATA {
 <R>
  <id> "bikeracks-geojson" ;
  <type> "FeatureCollection" ;
  <crs> [
    <type> "name" ;
    <properties> [
      <name> "urn:ogc:def:crs:OGC:1.3:CRS84" ;
    ] ;
  ] ;
}

INSERT {
 <R> <features> ?x
}
WHERE {
<C> <in> ?x
}

SELECT * WHERE {
VALUES ( ?root ) { ( <R> ) }
}