PREFIX afn: <http://jena.apache.org/ARQ/function#>
PREFIX db: <http://dbpedia.org/ontology/>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX prov: <http://www.w3.org/ns/prov#>
PREFIX qo: <http://qrowd-project.eu/ontology/>
PREFIX lgdo: <http://linkedgeodata.org/ontology/>
PREFIX qr: <http://qrowd-project.eu/resource/>

INSERT DATA {
  qr:dataset-linkedgeodata db:rank 2 .
  qr:dataset-trento-bikeracks db:rank 1 .
  qr:dataset-virtual-city-explorer-bike-racks db:rank 3 .
}

# Create Clusters
INSERT {
  ?cluster
    a qo:SameAsCluster ;
    qo:member ?member ;
    .
} WHERE {
  {
    SELECT ?member (group_concat(?o) AS ?cs) {
      {
        SELECT ?member ?o {
          ?member
            a lgdo:BicycleParking ;
            (owl:sameAs|^owl:sameAs)* ?o ;
            .
        } ORDER BY ?member ?o
      }
    } GROUP BY ?member
  }
  BIND(iri(concat(str(qr:), md5(encode_for_uri(?cs)))) AS ?cluster)
}

# Debug Cluster
# SELECT * {
#   ?s
#     qo:member ?o ;
#     a qo:SameAsCluster ;
#     .
# } ORDER BY ?s



# Add all clusters' members properties reificated to cluster
INSERT {
  ?cluster
    qo:memberProperty ?memberProperty ;
    .
  ?memberProperty
    a qo:MemberProperty ;
    owl:annotatedSource ?s ;
    owl:annotatedProperty ?p ;
    owl:annotatedTarget ?o ;
    .
} WHERE {
  ?cluster
    a qo:SameAsCluster ;
    qo:member ?s ;
    .
  ?s ?p ?o .
  BIND(iri(concat(str(qr:), struuid())) AS ?memberProperty)
}

# Debug cluster properties
# SELECT * {
#   ?s
#     qo:memberProperty ?mP ;
#     a qo:SameAsCluster ;
#     .
#   ?mP ?p ?o
# } ORDER BY ?s ?mP ?p ?o

# Add Representand
INSERT {
  ?cluster
    qo:representedBy ?rep ;
    .
  ?rep
    a qo:Representand ;
    .
} WHERE {
  ?cluster
    a qo:SameAsCluster ;
    .
  BIND(iri(concat(str(qr:), struuid())) AS ?rep)
}

# Add best ranked property to Representand
INSERT {
  ?rep
    ?p ?o ;
    qo:propertySource ?memberProperty ;
    .
} WHERE {
  {
    SELECT ?rep ?p (max(?rank) AS ?maxRank) {
      ?cluster
        qo:representedBy ?rep ;
        qo:memberProperty ?memberProperty ;
        .
      ?memberProperty
        owl:annotatedSource/prov:wasDerivedFrom/db:rank ?rank ;
        owl:annotatedProperty ?p ;
        .
    } GROUP BY ?rep ?p
  }
  ?cluster
    qo:representedBy ?rep ;
    qo:memberProperty ?memberProperty ;
    .
  ?memberProperty
    owl:annotatedSource/prov:wasDerivedFrom/db:rank ?rank ;
    owl:annotatedProperty ?p ;
    owl:annotatedTarget ?o ;
    .
  FILTER(?rank = ?maxRank)
  FILTER(?p != prov:wasDerivedFrom)
}

# Debug type
# SELECT * {
#   ?s
#     a qo:Representand ;
#     ?p ?o ;
# } ORDER BY ?s ?p ?o

# Update capacity
DELETE {
  ?s lgdo:capacity ?p ;
} WHERE {
  ?s a qo:Representand ;
    lgdo:capacity ?p ;
}

INSERT {
  ?rep lgdo:capacity ?capacity ;
} WHERE {
  SELECT ?rep (sum(xsd:integer(?cap)) AS ?capacity) {
    ?rep
      a qo:Representand ;
      qo:propertySource ?memberProperty ;
      .
    ?memberProperty
      owl:annotatedProperty lgdo:capacity ;
      owl:annotatedTarget ?cap ;
  } GROUP BY ?rep
}

CONSTRUCT WHERE { ?s ?p ?o }
